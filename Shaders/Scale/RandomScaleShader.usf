#include "/Engine/Private/Common.ush"
#include "../Common.ush"

RWStructuredBuffer<uint3> DirtyFlagBuffer;
RWStructuredBuffer<float4x4> OutputInstanceTransformBuffer;
StructuredBuffer<float4x4> InputInstanceTransformBuffer;
StructuredBuffer<float4x4> InitialTransformBuffer;

[numthreads(128, 1, 1)]
void RandomScaleCS(uint3 ThreadId : SV_DispatchThreadID)
{
	const float4x4 InfluencerTransform = RandomScaleShaderUniform.InfluencerTransform;
	const float4x4 ModelTransform      = RandomScaleShaderUniform.ModelTransform;
	const float3   TranslationInertia  = RandomScaleShaderUniform.TranslationInertia;
	const float3   RotationInertia     = RandomScaleShaderUniform.RotationInertia;
	const float3   ScaleInertia        = RandomScaleShaderUniform.ScaleInertia;
	
	const float3   MinScale            = RandomScaleShaderUniform.MinScale;
	const float3   MaxScale            = RandomScaleShaderUniform.MaxScale;
	const float    InfluencerRadius    = RandomScaleShaderUniform.InfluencerRadius;
    
	const uint Index = ThreadId.x;
	uint Size, Stride;
	InputInstanceTransformBuffer.GetDimensions(Size, Stride);
    
	if (Index < Size)
	{
		float4x4 InitialLocalTransform  = transpose(InitialTransformBuffer[Index]);
		float4x4 InstanceLocalTransform = transpose(InputInstanceTransformBuffer[Index]);
		
		float3 InitialT, InitialS;
		float3 InstanceT, InstanceS;
		float3 InfluencerT, InfluencerS;
		
		float4 InitialR;
		float4 InstanceR;
		float4 InfluencerR;
        
		decompose(InitialLocalTransform, InitialT, InitialR, InitialS);
		decompose(InstanceLocalTransform, InstanceT, InstanceR, InstanceS);
		decompose(InfluencerTransform, InfluencerT, InfluencerR, InfluencerS);
		
		float4 InstanceWorldT = mul(float4(InstanceT, 1), ModelTransform);
		InstanceWorldT.xyz *= rcp(InstanceWorldT.w);
		
		float Distance = length(InfluencerT - InstanceWorldT.xyz);
		
		if (Distance < InfluencerRadius)
		{
			float Alpha = nrand(Index) * Distance / InfluencerRadius;			
			float3 TargetScale = lerp(MinScale, MaxScale, Alpha);
			InstanceS = lerp(InstanceS, TargetScale, ScaleInertia);
		
			OutputInstanceTransformBuffer[Index] = compose(InstanceT, InstanceR, InstanceS);
			DirtyFlagBuffer[Index].z = 1;
		}
	}
}